ROUTINE MAJQTE
INIT
 K ^MODROUTINE
 w !,"Traitement de l'historique des commandes passées"
 w !,"------------------------------------------------"
 S NBOCC1=$$HWHISCOM("")
 w !,!,"Traitement de l'historique des quantités reçues"
 w !,"-----------------------------------------------"
 S NBOCC2=$$HWHISLIV("")
 w !,!,"Terminé !!!",!
 S MAG=$G(^HWINFO("MAG"))
 S ^MODROUTINE(MAG,"MAJQTE","HWHISCOM")=NBOCC1
 S ^MODROUTINE(MAG,"MAJQTE","HWHISLIV")=NBOCC2
 Q
HWHISCOM(VARTMP)
 K ^HWHISCOM
 S CPT=0
 S CMDNUM=""
 F  S CMDNUM=$O(^HWARCCMD("CDE",CMDNUM)) Q:(CMDNUM="")  D
 .S LIGNE=""
 .F  S LIGNE=$O(^HWARCCMD("CDE",CMDNUM,LIGNE)) Q:(LIGNE="")  D
 ..I +LIGNE=0 D
 ...S DATECMD=$P(^HWARCCMD("CDE",CMDNUM,LIGNE),"\",1)
 ...S ANNEECMD=$E(DATECMD,1,4)
 ...S MOISCMD=+$E(DATECMD,5,6)
 ...Q
 ..I +LIGNE>0 D
 ...S ARTICLE=$P(^HWARCCMD("CDE",CMDNUM,LIGNE),"\",1)
 ...S QTECMD=+$P(^HWARCCMD("CDE",CMDNUM,LIGNE),"\",2)
 ...S CPT=CPT+1
 ...S ^HWHISCOM(ARTICLE,"TOTAL")=+$G(^HWHISCOM(ARTICLE,"TOTAL"))+QTECMD
 ...S ^HWHISCOM(ARTICLE,ANNEECMD)=+$G(^HWHISCOM(ARTICLE,ANNEECMD))+QTECMD
 ...S ^HWHISCOM(ARTICLE,ANNEECMD,MOISCMD)=+$G(^HWHISCOM(ARTICLE,ANNEECMD,MOISCMD))+QTECMD
 ...Q
 ..Q
 .Q
 Q CPT
HWHISLIV(VARTMP)
 K ^HWHISLIV
 S CPT=0
 S CMDFAX=""
 F  S CMDFAX=$O(^HWARCCMD("LIVR",CMDFAX)) Q:(CMDFAX="")  D
 .S LIGNEFAX=""
 .F  S LIGNEFAX=$O(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX)) Q:(LIGNEFAX="")  D
 ..S CMDNUM=""
 ..F  S CMDNUM=$O(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM)) Q:(CMDNUM="")  D
 ...S LIGNENUM=""
 ...F  S LIGNENUM=$O(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM,LIGNENUM)) Q:(LIGNENUM="")  D
 ....S BLNUM=""
 ....F  S BLNUM=$O(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM,LIGNENUM,BLNUM)) Q:(BLNUM="")  D
 .....S DATELIVR=$P(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM,LIGNENUM,BLNUM),"\",5)
 .....I +DATELIVR>0 D
 ......S ANNEELIVR=$E(DATELIVR,1,4)
 ......S MOISLIVR=+$E(DATELIVR,5,6)
 ......I +CMDNUM=0 S ARTICLE=$P(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM,LIGNENUM,BLNUM),"\",6)
 ......I +CMDNUM>0 S ARTICLE=$P($G(^HWARCCMD("CDE",CMDNUM,LIGNENUM)),"\",1)
 ......S QTELIV=+$P(^HWARCCMD("LIVR",CMDFAX,LIGNEFAX,CMDNUM,LIGNENUM,BLNUM),"\",2)
 ......S CPT=CPT+1
 ......S ^HWHISLIV(ARTICLE,ANNEELIVR)=+$G(^HWHISLIV(ARTICLE,ANNEELIVR))+QTELIV
 ......S ^HWHISLIV(ARTICLE,ANNEELIVR,MOISLIVR)=+$G(^HWHISLIV(ARTICLE,ANNEELIVR,MOISLIVR))+QTELIV
 ......S ^HWHISLIV(ARTICLE,"TOTAL")=+$G(^HWHISLIV(ARTICLE,"TOTAL"))+QTELIV
 ......Q
 .....Q
 ....Q
 ...Q
 ..Q
 .Q
 Q CPT
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 